library SpellIllusionBlink requires optional HandleHT {
    /* === Настройки заклинания === */

    constant integer blinkId = 'SEbl'; // Ид кастомного блинка
    constant integer illusionId = 'CIil';

    /* === Реализация заклинания === */
    unit caster, illusionUnit;
    integer level;
    ability illusionAbility;
    trigger trig;

    function onInit() {
        trigger t = CreateTrigger();
        integer i;

        // Слушаем событие каста для всех игроков, экономя лишний вызов функции
        for (0 <= i < bj_MAX_PLAYER_SLOTS) TriggerRegisterPlayerUnitEvent(t, Player(i), EVENT_PLAYER_UNIT_SPELL_EFFECT, null);
        TriggerAddAction(t, function() {
            if (blinkId != GetSpellAbilityId()) return; // Используем return чтоб не использовать Condition
            caster = GetTriggerUnit();

            // Создаём динамический триггер для того, чтоб отследить иллюзию
            trig  = CreateTrigger();
            TriggerRegisterUnitEvent(trig, caster, EVENT_UNIT_SUMMON);
            TriggerAddAction(trig, function(){
                caster = GetSummoningUnit();
                illusionUnit = GetSummonedUnit();
                // Позиционируем иллюзию на место кастера
                SetUnitX(illusionUnit, GetUnitX(caster));
                SetUnitY(illusionUnit, GetUnitY(caster));
                SetUnitFacingEx(illusionUnit, GetUnitFacing(caster), true);

                // Уничтожаем динамический триггер
                trig = GetTriggeringTrigger();
                TriggerClearActions(trig);
                DestroyTrigger(trig);
            });

            // Добавляем способность иллюзий и кастуем её
            if (GetUnitAbilityLevel(caster, illusionId) == 0) UnitAddAbility(caster, illusionId);
            SetUnitAbilityLevel(caster, illusionId, GetUnitAbilityLevel(caster, blinkId));
            illusionAbility = GetUnitAbility(caster, illusionId);
            ShowAbility(illusionAbility, false); // Скрываем способность создания иллюзии с панели приказов
            CastAbilityTarget(illusionAbility, caster); // Применяем скрытую способность на кастера для создания иллюзии

        });

        t = null;
    }
}
